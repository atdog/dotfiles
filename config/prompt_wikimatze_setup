function prompt_wikimatze_precmd {
  git-info
}

function prompt_wikimatze_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_wikimatze_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_mseri_colors=(
      "%F{81}"  # Turquoise
      "%F{87}"  # Pastel Cyan
      "%F{215}" # Light Orange
      "%F{166}" # Orange - not used
      "%F{119}" # Pastel Limegreen
      "%F{248}" # Grey
      "%F{196}" # Red
      "%F{228}" # Yellow
    )
  else
    _prompt_mseri_colors=(
      "%F{cyan}"
      "%F{magenta}"
      "%F{yellow}"
      "%F{red}"
      "%F{green}"
      "%F{grey}"
      "%F{red}"
      "%F{yellow}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '${_prompt_mseri_colors[3]}%b%f'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'

  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}◼%f%%b'

  zstyle ':prezto:module:git:info:keys' format \
      'prompt' ' ($(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s)' \
      'rprompt' ''

  # Define prompts.
  PROMPT="${_prompt_mseri_colors[6]}%m%f ${_prompt_mseri_colors[8]}%c%f"'${(e)git_info[prompt]}'" ${_prompt_mseri_colors[7]}➜%f "
  RPROMPT=''

}

prompt_wikimatze_setup "$@"
